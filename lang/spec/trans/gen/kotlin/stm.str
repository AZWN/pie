module gen/kotlin/stm

imports

  signatures/-
  signatures/lex/-
  signatures/impl/-
  
  statsem/program
  
  gen/kotlin/exp
  gen/kotlin/binder
  gen/kotlin/type
  gen/idtype-tuples
  gen/util
  gen/debug

rules

  try-p2k-stm    = pie-try-debug(p2k-stm|"p2k-stm") <+ !"{}"
  p2k-stms       = p2k-stms(|"\n")
  p2k-stms(|sep) = map(try-p2k-stm); separate-by(|sep); concat-strings

rules

  p2k-stm:
    Block(stms) -> 
$[{
  [str]
}]
    with
      str := <p2k-stms> stms

rules // Value declaration

  p2k-stm:
    ValDec(binder, exp) -> $[val [binderStr] = [expStr]]
    with
      binderStr := <p2k-binder> binder
    ; expStr    := <try-p2k-exp> exp

rules // If else statement

  p2k-stm:
    IfStm(condExp, stm) -> $[if([condExpStr]) [stmStr]]
    with
      condExpStr := <try-p2k-exp> condExp
    ; stmStr     := <try-p2k-stm> stm

  p2k-stm:
    IfElseStm(condExp, trueStm, falseStm) -> $[if([condExpStr]) [trueStmStr] else [falseStmStr]]
    with
      condExpStr  := <try-p2k-exp> condExp
    ; trueStmStr  := <try-p2k-stm> trueStm
    ; falseStmStr := <try-p2k-stm> falseStm

rules // List comprehension

  p2k-stm:
    ListComprehensionStm(mapStm, binder, listExp) -> $[[listExpStr].forEach {[binderStr] -> [mapStmStr]}]
    with
      mapStmStr  := <try-p2k-stm> mapStm
    ; binderStr  := <p2k-binder> binder
    ; listExpStr := <try-p2k-exp> listExp

rules // Path operations

  p2k-stm:
    RequiresPath(exp, filterOpt, stamper) -> $[require([expStr], [stamperStr])]
    with
      expStr     := <p2k-exp> exp
    ; stamperStr := <p2k-stamper> (filterOpt, stamper)

  p2k-stm:
    GeneratesPath(exp, stamper) -> $[generate([expStr], [stamperStr])]
    with
      expStr     := <p2k-exp> exp
    ; stamperStr := <p2k-stamper> (None(), stamper)
  
  p2k-stamper:
    (None(), Modified()) -> "PathStampers.modified"
  p2k-stamper:
    (None(), Hash()    ) -> "PathStampers.hash"
  p2k-stamper:
    (filter, Modified()) -> $[PathStampers.modified([filterStr])]
    where
      <not(?None())> filter
    with
      filterStr := <p2k-filter-match> filter
  p2k-stamper:
    (filter, Hash()) -> $[PathStampers.hash([filterStr])]
    where
      <not(?None())> filter
    with
      filterStr := <p2k-filter-match> filter

rules // Return and fail

  p2k-stm:
    Return(exp) -> $[return output([expStr])]
    where
      <pie-ast-type; (?TupleTy(_) <+ NullableTy(?TupleTy(_)))> exp
    with
      expStr := <try-p2k-exp> exp

  p2k-stm:
    Return(exp) -> $[return [expStr]]
    where
      <pie-ast-type; not(?TupleTy(_))> exp
    with
      expStr := <try-p2k-exp> exp


  p2k-stm:
    Fail(exp) -> $[throw ExecException([expStr].toString())]
    with
      expStr := <try-p2k-exp> exp

rules // Expression as statement

  p2k-stm:
    Exp(exp) -> $[[expStr]]
    with
      expStr := <try-p2k-exp> exp
