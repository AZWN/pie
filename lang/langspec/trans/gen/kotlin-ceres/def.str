module gen/kotlin-ceres/def

imports

  libspoofax/stratego/debug

  signatures/-
  signatures/lex/-
  signatures/impl/-
  
  gen/kotlin-ceres/stm
  gen/kotlin/type2kotlin
  gen/idtype-tuples
  gen/util

rules // definitions

  p2c-def:
    FuncDef(FuncHead(funcId, params, type), FuncImpl(Block(stms))) -> str
    with
      className   := <pipe-sanitize-class-id> funcId
    with
    {| InputKind:
      (inputName , inputDataClass)  := <p2c-funcdef-inputsig(|className)> params
    ; (outputName, outputDataClass) := <p2c-funcdef-outputsig(|className)> type
    ; buildImpl   := <p2c-stms> stms
    ; emptyReturn := <?VoidTy(); !"return Unit.instance" <+ !""> type
   |}
    with
      rules(ModuleBindings :+= $[binder.bindBuilder<[className]>(builders, "[className]")])
    with
      str := 
$[class [className] : Builder<[inputName], [outputName]> {
  [inputDataClass][outputDataClass]override val id = "[className]"
  override fun BuildContext.build(input: [inputName]): [outputName] {
    [buildImpl]
  }
}
]

  p2c-funcdef-inputsig(|className):
    Params(params) -> ("None", "")
    where
      0 := <length> params
    with 
      rules(InputKind := 0)
      
  p2c-funcdef-inputsig(|className):
    p@Params(params) -> (tySig, "")
    where
      1 := <length> params
    with
      [(_, ty)] := <pipe-idtype-tuples> p
    ; tySig     := <t2k-sig> ty
    with 
      rules(InputKind := 1)
    
  p2c-funcdef-inputsig(|className):
    p@Params(params) -> ($[[className].Input], $[[dataClassStr]["\n"]])
    where
      length := <length> params
    ; <gt> (length, 1)
    with
      idTypeTuples := <pipe-idtype-tuples> p
    ; dataClassStr := <t2k-generate-dataclass(|"Input")> idTypeTuples
    with 
      rules(InputKind := 2)

    
  p2c-funcdef-outputsig(|className):
    ty -> (<t2k-sig> ty, "")
    where
      <not(?TupleTy(_)); not(?NullableTy(TupleTy(_)))> ty
      
  p2c-funcdef-outputsig(|className):
    ty -> ($[[className].Output], $[[dataClassStr]["\n"][outputFun]["\n\n"]])
    where
      <?TupleTy(_)> ty
    with
      idTypeTuples := <pipe-idtype-tuples> ty
    ; dataClassStr := <t2k-generate-dataclass(|"Output")> idTypeTuples
    ; outputFun    := $[private fun output(tuple: [<t2k-sig> ty]) = Output(tuple)]

  p2c-funcdef-outputsig(|className):
    ty -> ($[[className].Output?], $[[dataClassStr]["\n"][outputFun]["\n\n"]])
    where
      <?NullableTy(tty@TupleTy(_))> ty
    with
      idTypeTuples := <pipe-idtype-tuples> tty
    ; dataClassStr := <t2k-generate-dataclass(|"Output")> idTypeTuples
    ; outputFun    := $[private fun output(tuple: [<t2k-sig> ty]) = if(tuple == null) null else Output(tuple)]
