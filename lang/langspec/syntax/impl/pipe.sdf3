module impl/pipe

imports

  lex/literal
  type
  func

context-free syntax

  DataImpl =

context-free syntax

  FuncImpl.FuncImpl = <<Block>>

context-free syntax

  Block.Block = <{
  <{Stm "\n"}*>
}>
  Stm = <<Block>>

  Binder.SingleBinder = <<Bind>>
  Binder.TupleBinder  = <(<{Bind ", "}*>)>

  Bind.Bind   = <<ID>>
  Bind.TBind = <<ID> : <Type>>

  Stm.VarDecBinder = <var <Binder> = <Exp>;>
  Stm.VarDec = <var <ID> : <Type>;>
  Stm.Assign = <<Ref> = <Exp>;>

  Stm.If = <if(<Exp>) <Stm>>
  Stm.IfElse = <if(<Exp>) <Stm> else <Stm>>
  Stm.ForEach = <foreach(var <Binder> in <Exp>) <Stm>>
  
  Stm.Return = <return <Exp>;>
  Stm.Fail = <fail <Exp>;>
  
  Stm.Exp = <<Exp>;>

context-free syntax

  Exp.ToNullable = <<Exp>?>
  Exp.ToNonNullable = <<Exp>!>

  Exp.Eq = <<Exp> == <Exp>> {left}
  Exp.Neq = <<Exp> != <Exp>> {left}
  Exp.Lor = <<Exp> || <Exp>> {left}
  Exp.Land = <<Exp> && <Exp>> {left}

  Ref.Ref = <<ID>> {avoid}
  Exp = Ref
  
  Exp.Call = <<FUNCID>(<{Exp ", "}*>)>
  Exp.QCall = <<Exp>.<FUNCID>(<{Exp ", "}*>)>

  Exp.BoolLit = <<Bool>>
  Bool.True   = <true>
  Bool.False  = <false>

  Exp.IntLit  = <<INT>>
  Exp.StrLit  = <<STRING>>

  Exp.NullLit = <null>

  Exp.TupleLit = <(<{Exp ","}*>)>
  Exp.ListLit  = <[<{Exp ","}*>]>
  Exp.ListAppendLit  = <[<Exp> | <{Exp ","}+>]>

  Exp = <(<Exp>)> {bracket}

context-free priorities

  { left: Exp.Eq Exp.Neq } > { left: Exp.Lor Exp.Land }

lexical syntax

  FUNCID = "return" {reject}
  FUNCID = "foreach" {reject}
